buildscript {
    ext {
        springBootVersion = '1.3.0.BUILD-SNAPSHOT'
    }
    repositories {
        jcenter()
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "com.moowork.gradle:gradle-node-plugin:0.11" // for Node & Npm
    }
}

version "0.1"
group "com.tammolaube"

apply plugin: "groovy"
apply plugin: "spring-boot"
apply plugin: "application"
apply plugin: "com.moowork.node" // for Node, Npm on which Bower depends

ext {
    npmModules = 'node_modules'
    npmConfig = 'package.json'
}

springBoot {
    mainClass = "com.tammolaube.site.Application"
}

node {
    version = '4.1.2'
    npmVersion = '2.14.4'
    download = false
}

tasks.withType(org.springframework.boot.gradle.run.BootRunTask) {
    systemProperty('spring.profiles.active', 'development')
}

task npmCacheConfig(type: NpmTask) { // Node & Npm for Bower
    description = "Configure the NPM cache"
    def npmCacheDir = "${gradle.getGradleUserHomeDir()}/caches/npm"
    outputs.files file(npmCacheDir)
    args = [ 'config', 'set', 'cache', npmCacheDir ]
}

task npmPackages(type: NpmTask, dependsOn: npmCacheConfig) {
    description = "Install Node.js packages"
    args = [ 'install' ]
    inputs.files file(npmConfig)
    outputs.files file(npmModules)
}

clean.delete << file(npmModules)

task buildStaticResources(type: NpmTask, dependsOn: npmPackages) {
    args = ['run', 'build', '-s']
}

task buildStaticResourcesVerbose(type: NpmTask, dependsOn: npmPackages) {
    args = ['run', 'build']
}

repositories {
    jcenter()
    maven { url "http://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/milestone" }
}

dependencies {
    compile "org.codehaus.groovy:groovy"
    compile "org.codehaus.groovy:groovy-templates"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"

    runtime "com.h2database:h2"

    testCompile "org.springframework.boot:spring-boot-starter-test"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}
