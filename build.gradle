buildscript {
    ext {
        springBootVersion = '1.3.0.BUILD-SNAPSHOT'
    }
    repositories {
        jcenter()
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "com.moowork.gradle:gradle-node-plugin:0.11" // for Node & Npm
    }
}

version "0.1"
group "com.tammolaube"

apply plugin: "groovy"
apply plugin: "spring-boot"
apply plugin: "application"
apply plugin: "com.moowork.node" // for Node, Npm on which Bower depends

ext {
    staticResources = 'src/main/resources/static'
    npmModules = 'node_modules'
    npmConfig = 'package.json'
    bowerComponents = 'bower_components'
    bowerConfig = 'bower.json'
    bowerSassTarget = "src/main/sass/bower_components"
}

springBoot {
    mainClass = "com.tammolaube.site.Application"
}

node {
    version = '4.1.2'
    npmVersion = '2.14.4'
    download = false
}

task npmCacheConfig(type: NpmTask) { // Node & Npm for Bower
    description = "Configure the NPM cache"
    def npmCacheDir = "${gradle.getGradleUserHomeDir()}/caches/npm"
    outputs.files file(npmCacheDir)
    args = [ 'config', 'set', 'cache', npmCacheDir ]
}

task npmPackages(type: NpmTask, dependsOn: npmCacheConfig) {
    description = "Install Node.js packages"
    args = [ 'install' ]
    inputs.files file(npmConfig)
    outputs.files file(npmModules)
}

task bowerInstall(type: NodeTask, dependsOn: npmPackages) {
    script = file('node_modules/bower/bin/bower')
    args = ["--config.storage.cache=${gradle.getGradleUserHomeDir()}/caches/bower/cache",
            "--config.storage.packages=${gradle.getGradleUserHomeDir()}/caches/bower/packages",
            "--config.storage.registry=${gradle.getGradleUserHomeDir()}/caches/bower/registry",
            'install']
    inputs.files file(bowerConfig)
    outputs.files file(bowerComponents)
}

task bowerSyncScss(type: Sync, dependsOn: bowerInstall) {
    from 'bower_components/bootstrap/scss/'
    into bowerSassTarget
}

task sass(type: NodeTask, dependsOn: bowerSyncScss) {
    script = file('node_modules/node-sass/bin/node-sass')
    args = ['src/main/sass/bundle.sass', '-o', 'src/main/resources/static/css']
}

task browserify(type: NodeTask, dependsOn: npmPackages) {
    script = file('node_modules/browserify/bin/cmd.js')
    args = ['src/main/browserify/main.js', '-o', 'src/main/resources/static/js/bundle.js']
}

clean.delete << file(staticResources)
clean.delete << file('bower_components')
clean.delete << file('node_modules')

repositories {
    jcenter()
    maven { url "http://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/milestone" }
}

dependencies {
    compile "org.codehaus.groovy:groovy"
    compile "org.codehaus.groovy:groovy-templates"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.grails:gorm-hibernate4-spring-boot:1.0.0.RELEASE"

    runtime "com.h2database:h2:1.3.173"

    testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}
